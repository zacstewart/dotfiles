set nocompatible                                " turn off VI compatibility mode
call pathogen#infect()                          " load Pathogen bundles
call pathogen#helptags()
filetype plugin indent on                       " use filetype detection and load plugins and indent for them
set directory^=$HOME/.vim/.swp//                " centralize swp file location (trailing / is important)
runtime macros/matchit.vim
set hidden                                      " hide unsaved buffers instead of closing them

" Performance tweaks
set ttyfast
set lazyredraw

" Appearance
syntax on                                       " turn on syntax highlighting
set t_Co=256                                    " use 256 colors
set background=dark                             " dark background
colorscheme solarized

" Text formatting
set ai                                          " auto-indent
set si                                          " smart indentation
au! FileType python setl nosmartindent          " don't smart indent python
set expandtab                                   " use soft tabs
set softtabstop=2                               " that are 2 spaces wide
set shiftwidth=2                                " auto-indent and shift by 2 spaces
set tabstop=2                                   " make hard tabs appear 2 spaces when present
set list                                        " show non-printed chars
set listchars=tab:▸\ ,trail:·                   " specifically, tabs and trailing whitespace
set nowrap                                      " turn off line wrapping

" UI
set hlsearch                                    " highlight search terms
set mouse=a                                     " use mouse in all modes
set wildmode=longest,list,full                  " tab completeion: 1) bash-like, 2) list, 3) wildmenu
set wildmenu                                    " use wildmenu (3rd tab)
set wildignore+=/venv/*,*.pyc,.root             " don't wildmenu these
set number                                      " turn on line numbers
set cursorline                                  " highlight current line
set laststatus=2                                " always show statusline
set ruler                                       " and show the line and column number in it
set noshowmode                                  " but not mode
set clipboard=unnamedplus                       " use OS clipboard by default
set backspace=indent,eol,start                  " let me delete whitespace, damn it
set splitbelow                                  " make new splits appear below
set splitright                                  " make new vertical splits appear to the right

set tags=.git/tags                              " ctags

" Set cursor to a line in edit mode
if !has('nvim')
  if exists('$TMUX')
    let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
    let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
    set ttymouse=xterm2                         " enable mouse control in tmux
  else
    let &t_SI = "\<Esc>]50;CursorShape=1\x7"
    let &t_EI = "\<Esc>]50;CursorShape=0\x7"
  endif
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Custom key mapping
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
inoremap jj <Esc>
" How many times must I insert jJ:w?
inoremap jJ <Esc>

nnoremap X $x
" Make Y behave like D
nnoremap Y y$
" remove superfluous whitespace
map <Leader><Space> :%s/\s\+$//g<CR>
" Visual indent
vnoremap < <gv
vnoremap > >gv

" Tmux-like navigation
nnoremap <silent> <C-w>c :tabnew<CR>
nnoremap <silent> <C-w>n :tabnext<CR>
nnoremap <silent> <C-w>p :tabprevious<CR>
nnoremap <silent> <C-w>N :tabmove +1<CR>
nnoremap <silent> <C-w>P :tabmove -1<CR>

nnoremap <silent> <C-N> :Dispatch git grep --no-color <C-R><C-W><CR>

" ctrl-{a,e} for jumping to {begnning,end} of command mode line
cnoremap <C-a> <Home>
cnoremap <C-e> <End>

" Get rid of search highlighting when done with it
nnoremap <silent> <C-h> :noh<CR>

" manually define filetypes
autocmd BufNewFile,BufRead  *.scss  set filetype=scss
autocmd BufNewFile,BufRead  scp://*  set cmdheight=2

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" File exploring
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <Leader>e :Explore<CR>
nnoremap <Leader>d :Vexplore<CR>
nnoremap <Leader>t :Texplore<CR>
let g:netrw_winsize = 20

""
" Opens a file fuzzy-finder to search files listed in $PWD/.file-index.
"
" Enter opens the file in the current split
" ctrl-t opens it in a new tab
" ctrl-v opens in a virtical split
" ctrl-x opens in a horizontal spit
function! FindFile()
  if !filereadable('.file-index')
    call ReindexFiles()
  endif


  if exists('$TMUX')
    let l:fzf = 'fzf-tmux'
  else
    let l:fzf = 'fzf'
  endif

  try
    let l:response = system('cat .file-index | ' . l:fzf . ' --expect enter,ctrl-t,ctrl-v,ctrl-x')
  catch /Vim:Interrupt/
    redraw!
    return
  endtry

  redraw!

  let l:parts = split(l:response, "\n")
  if len(l:parts) < 2 | return | endif
  let l:cmd = l:parts[0]
  let l:file = l:parts[1]

  if l:cmd == "ctrl-t"
    execute ":tabnew " . l:file
  elseif l:cmd == "ctrl-v"
    execute ":vsplit " . l:file
  elseif l:cmd == "ctrl-x"
    execute ":split " . l:file
  else
    execute ":e " . l:file
  endif
endfunction

""
" Reindexes the files in $PWD using ag. It ignores gitignored files as well as
" some package manager artifacts
function! ReindexFiles()
  echo "Reindexing files..."
  let l:output = system('ag --nocolor --nogroup --ignore .git --ignore bower_components --ignore doc --ignore node_modules --ignore venv --hidden -l > .file-index')
  redraw!
  if v:shell_error
    echo "Couldn't index files: " . l:output
  else
    echo "Done"
  endif
endfunction

nnoremap <C-p> :call ReindexFiles()<CR>
nnoremap <C-p><C-p> :call FindFile()<CR>

let g:go_fmt_command = "goimports"
let g:go_metalinter_autosave = 1
let g:go_auto_type_info = 1

if !exists('g:vdebug_options')
    let g:vdebug_options = {}
endif
let g:vdebug_options["break_on_open"] = 0
let g:vdebug_options["timeout"] = 60

let s:vimrc_local = $HOME . '/.vimrc.local'
if filereadable(s:vimrc_local)
  execute "source " . s:vimrc_local
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugin Configuration                                                         "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Tabularize

nmap <Leader>a= :Tabularize /=<CR>
vmap <Leader>a= :Tabularize /=<CR>
nmap <Leader>a\/ :Tabularize /\/<CR>
vmap <Leader>a\/ :Tabularize /\/<CR>
nmap <Leader>a{ :Tabularize /{<CR>
vmap <Leader>a{ :Tabularize /{<CR>
nmap <Leader>a: :Tabularize /:\zs<CR>
vmap <Leader>a: :Tabularize /:\zs<CR>
nmap <Leader>a, :Tabularize /,\zs<CR>
vmap <Leader>a, :Tabularize /,\zs<CR>
nmap <Leader>a> :Tabularize /[=-]><CR>
vmap <Leader>a> :Tabularize /[=-]><CR>
nmap <Leader>a\| :Tabularize /\|<CR>
vmap <Leader>a\| :Tabularize /\|<CR>

" NERDCommenter

" Create default mappings
let g:NERDCreateDefaultMappings = 1

" Add spaces after comment delimiters by default
let g:NERDSpaceDelims = 1

" Align line-wise comment delimiters flush left instead of following code indentation
let g:NERDDefaultAlign = 'left'

" Turn on all Python syntax highlighting options, turn off Python 2 though
let g:python_highlight_all = 1
let g:python_version_2 = 0

# Indenting inside parents should only be one shiftwidth. It defaults to 2
let g:pyindent_open_paren=shiftwidth()
